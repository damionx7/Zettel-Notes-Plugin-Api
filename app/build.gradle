plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def appVersion = 25

def buttonApiVersion = 11
def buttonQueryIntent = "org.eu.thedoc.zettelnotes.intent.buttons"
def buttonMetadataApiVersionString = "org.eu.thedoc.zettelnotes.interfaces.version"

def scanApiVersion = 3
def scanQueryIntent = "org.eu.thedoc.zettelnotes.intent.scan"
def scanMetadataApiVersionString = "org.eu.thedoc.zettelnotes.interfaces.scan.version"

def zettelNotesPackageName = "org.eu.thedoc.zettelnotes"
def zettelNotesPackageNameDebug = "org.eu.thedoc.zettelnotes.debug"

android {
    namespace "org.eu.thedoc.zettelnotes.interfaces"
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode appVersion
        versionName "${appVersion}"
        //button interface
        resValue "string", "button_api_version", "\"${buttonApiVersion}\""
        buildConfigField("String", "BUTTON_API_VERSION", "\"${buttonApiVersion}\"")
        resValue "string", "plugin_button_query_intent", "\"${buttonQueryIntent}\""
        buildConfigField("String", "PLUGIN_BUTTON_QUERY_INTENT", "\"${buttonQueryIntent}\"")
        resValue "string", "plugin_button_metadata_api_string", "\"${buttonMetadataApiVersionString}\""
        buildConfigField("String", "PLUGIN_BUTTON_METADATA_API_STRING", "\"${buttonMetadataApiVersionString}\"")
        //scan interface
        resValue "string", "scan_api_version", "\"${scanApiVersion}\""
        buildConfigField("String", "SCAN_API_VERSION", "\"${scanApiVersion}\"")
        resValue "string", "plugin_scan_query_intent", "\"${scanQueryIntent}\""
        buildConfigField("String", "PLUGIN_SCAN_QUERY_INTENT", "\"${scanQueryIntent}\"")
        resValue "string", "plugin_scan_metadata_api_string", "\"${scanMetadataApiVersionString}\""
        buildConfigField("String", "PLUGIN_SCAN_METADATA_API_STRING", "\"${scanMetadataApiVersionString}\"")
        //package names needed for broadcast receiver
        resValue "string", "zettel_package_name", "\"${zettelNotesPackageName}\""
        resValue "string", "zettel_package_name_debug", "\"${zettelNotesPackageNameDebug}\""
        buildConfigField "String", "ZETTEL_PACKAGE_NAME", "\"${zettelNotesPackageName}\""
        buildConfigField "String", "ZETTEL_PACKAGE_NAME_DEBUG", "\"${zettelNotesPackageNameDebug}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            //disable proguard for plugin system to work
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles "consumer-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        multipleVariants {
            includeBuildTypeValues('release')
        }
    }
    buildFeatures {
        buildConfig true
    }

}

dependencies {
    api "androidx.appcompat:appcompat:1.7.0"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.damionx7'
                artifactId = 'Zettel-Notes-Plugin-Api'
                version = "${appVersion}"

                afterEvaluate {
                    from components.default
                }
            }
        }
    }
}